#############
#  ALIASES  #
#############

EDITOR=nvim

# abbreviation aliases
alias mkexec='chmod +x'

# common utility aliases
alias note="vim $(date +%F).md" # used a ton for taking notes

# convenience aliases for ubuntu-based systems
if [[ $(type apt) ]]
then
  alias python='python3'
  alias pip='pip3'
  alias py='bpython'
  # functions instead of alias for use inside this file
  function fd { fdfind $@ }
  function bat { batcat $@ --color=always }
fi

# standard aliases
alias rm='rm -I'                  # or possibly rm -Irf
alias cp='cp -i'
alias df='df -h'
alias ls='ls -h --color=yes'      # or exa
alias ll='ls -l'                  # ...
alias la='ls -a'                  # ...
alias lah='ls -la'                # ...
alias grep='grep --color=yes'     # or ripgrep (binary name is rg)
alias fgrep='fgrep --color=auto'  # ...
alias egrep='egrep --color=auto'  # ...
alias mkdir='mkdir -p'
alias tree='tree -CF' # or exa -T




##############
#  SETTINGS  #
##############

# - History
HISTFILE=$HOME/.history
HISTSIZE=1000
SAVEHIST=200
# - Behaviour
setopt bang_hist hist_ignore_space hist_beep beep
setopt share_history inc_append_history extended_history
setopt interactive_comments # correct_all 
# - Storage
setopt hist_find_no_dups hist_ignore_all_dups
setopt hist_fcntl_lock hist_reduce_blanks
setopt hist_no_functions
# - Directory Stack
setopt auto_cd
setopt auto_pushd 
setopt pushd_minus pushd_silent pushd_to_home pushd_ignore_dups 
DIRSTACKSIZE=13
# - Completion
setopt bash_auto_list list_packed list_rows_first list_types
setopt glob_complete auto_param_slash
# - Globbing
setopt glob extended_glob
setopt bad_pattern
setopt bare_glob_qual brace_ccl glob_dots ksh_glob mark_dirs
# - I/O
bindkey -v
bindkey '^?' backward-delete-char
bindkey '^h' backward-delete-char
bindkey '^[[Z' reverse-menu-complete
# - Style
setopt prompt_subst



###############
#  FUNCTIONS  #
###############

# opens two files to compare and modify
function merge { vimdiff $1 $2 }

# abbreviates directory fish style
function _shrink_path {
  local paths=(${PWD/$HOME/\~}) 'cur_dir'
  paths=(${(s:/:)paths})

  # TODO shorten all but the last path name by iterating the index
  len=$(( ${#paths[@]} - 1 ))
  for i in $(seq 1 $len )
  do
    cur_dir+="${paths[$i]:0:1}"
    cur_dir+='/'
  done
  cur_dir+="${paths[$(( -1 ))]}"
  print -P "$cur_dir"
}

# updates vcs info status
function _vcs_show {
  setopt prompt_subst
  autoload -Uz vcs_info
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' stagedstr '+'
  zstyle ':vcs_info:*' unstagedstr '~'
  zstyle ':vcs_info:*' patch-format "%10>..>%p%<< (%n applied)"
  zstyle ':vcs_info:*' nvcsformats "%F{yellow}%B$(_shrink_path)%b%f"
  zstyle ':vcs_info:*' formats '%F{yellow}%B%r/%S%%b%f %F{cyan}%b%f %F{green}%c%f %F{yellow}%u%f'
  vcs_info
}

function preexec {
  timer=$(( $(date +%s%0N) / 1000000000 )) # start timer
}

function precmd {
  _vcs_show # display directory/git info

  # calculate and show how much time has passed since last command execution
  if [ $timer ]
  then
    now=$(( $(date +%s%0N) / 1000000000 ))
    elapsed=$(( $now - $timer ))  # elapsed time in milliseconds
    delta=$(print -P "\U0394")

    threshold=3   # seconds to display elapsed time
    if [[ $(( $elapsed )) -gt $threshold ]]
    then
      export RPROMPT="%F{cyan}$delta${elapsed}s%f "
    else
      export RPROMPT=""
    fi
  fi
  unset timer
}




##############
#  THEMEING  #
##############

# AUTOLOADS
autoload -Uz compinstall  # && compinstall
autoload -U colors && colors
autoload -Uz compinit
autoload -Uz vcs_info
zstyle :compinstall filename '/home/jaybee/.zshrc'

# COLORS AND SYMBOLS
elem=`print "\U220a"`
userhost='%(!|%F{red}|%F{green})%B%n@%m%b%f'
errcode="%(?||%F{red}â†¯%? )%f"
# indicate subshell levels
if [[ -n "$NNNLVL" ]]
then
  symbol='â™ž '
  level="%B%F{yellow}%(2L|$symbol|)%b%f"
else
  symbol='ðŸ–¥'
  if [[ $TMUX ]]
  then
    level="%B%F{yellow}%(3L|$(($SHLVL - 1))$symbol|)%b%f"
  else
    level="%B%F{yellow}%(2L|%L$symbol|)%b%f"
  fi
fi

state=`print "âŸ¨Î¨$levelâŸ©"`

# PROMPTS
PROMPT='$userhost $elem ${vcs_info_msg_0_}
$errcode$state%f '



#########
#  ENV  #
#########

# set environment variables taking system state into consideration
PATH="$PATH:$HOME/.local/bin:$HOME/bin"
[[ $(type emacs) ]] && PATH="$PATH:$HOME/.emacs.d/bin"
[[ $(type cabal) ]] && PATH="$PATH:$HOME/.cabal/bin"
[[ $(type perl) ]] && PATH="$PATH:$HOME/.perl5/bin"  # needed for perl5
[[ $(type tmux) ]] && PATH="$PATH:$HOME/.config/tmux/scripts"
[[ -f /usr/local/cuda/bin ]] && PATH="$PATH:/usr/local/cuda/bin"  # needed for nvidia cuda+cudnn

export PATH
export LC_CTYPE="en_CA.UTF-8"
export EDITOR="nvim"
export OPENER="xdg-open"



############
# PROGRAMS #
############

# with ripgrep don't forget ripgrep-all (rga)

# FZF
if [[ $(type fzf) ]]
then
  # doesn't seem to work for some reason when using fd or bat despite function definitions above
  export FZF_COMPLETION_TRIGGER='**'
  export FZF_DEFAULT_OPTS="--prompt='ðŸ”­ ' --height 80% --layout=reverse --border"
  export FZF_DEFAULT_COMMAND="fdfind --type f --hidden --follow --exclude .git"

  # neovim + fzf = "Neovim File fuzzy search" -> nf
  function nf {
    # ensure neovim is installed
    [[ -z $(type nvim) ]] && return

    selection=$(fzf --preview 'batcat {} --color=always')
    if [[ -z "$selection" ]]
    then
      return
    else
      nvim $selection
    fi
  }

  # neovim + fzf + ripgrep = "Neovim Line fuzzy search" -> nl
  function nl {
    # ensure neovim and ripgrep are installed
    [[ -z $(type nvim) ]] && return
    [[ -z $(type rg) ]] && return

    # filter rg selections into fzf with preview
    selection=$(rg -n . | fzf -d ':' --preview 'batcat {1} --color=always --highlight-line {2}')
    if [[ -z "$selection" ]]
    then
      return
    else
      # split answer into line number and file name, then open there
      arr=(${(s/:/)selection})  # note: zsh specific syntax
      filename=$arr[1]
      linenum=$arr[2]
      nvim $(printf "+%s %s" $linenum $filename)
    fi
  }
fi

# FASD
if [[ $(type fasd) ]]
then
  eval "$(fasd --init posix-alias zsh-hook zsh-ccomp zsh-ccomp-install zsh-wcomp zsh-wcomp-install)"
  [[ -f ~/.fzf.zsh ]] && source ~/.fzf.zsh
  alias v='f -e $EDITOR'
  alias o='a -e $OPENER'
fi

# NNN
if [[ $(type nnn) ]]
then
  export NNN_FIFO=/tmp/nnn.fifo

  # bookmarks
  export NNN_BMS="h:$HOME;m:$HOME/Sync/Media;n:$HOME/Sync/Notes;p:$HOME/Sync/Projects;c:$HOME/Sync/Projects/Programming"

  # plugins
  export NNN_PLUG="p:preview-tui;n:nuke;m:cmusq;g:gitroot"

  # cd on quit, better alias
  # nnn_bin="$(which nnn)"
  function n {
    # Block nesting of nnn in subshells
    if [[ "${NNNLVL:-0}" -ge 1 ]]; then
        echo "nnn is already running"
        return
    fi

    # The behaviour is set to cd on quit (nnn checks if NNN_TMPFILE is set)
    # If NNN_TMPFILE is set to a custom path, it must be exported for nnn.
    # To cd on quit only on ^G, remove the "export" and make sure not to
    # use a custom path, i.e. set NNN_TMPFILE *exactly* as follows:
    #     NNN_TMPFILE="${XDG_CONFIG_HOME:-$HOME/.config}/nnn/.lastd"
    NNN_TMPFILE="${XDG_CONFIG_HOME:-$HOME/.config}/nnn/.lastd"

    # Unmask ^Q (, ^V etc.) (if required, see `stty -a`) to Quit nnn
    stty start undef
    stty stop undef
    stty lnext undef

    nnn $@

    if [ -f "$NNN_TMPFILE" ]; then
            . "$NNN_TMPFILE"
            rm -f "$NNN_TMPFILE" > /dev/null
    fi
  }

  alias nnn="n -e"
  alias NNN="sudo -E n -dH"
fi



# The following lines were added by compinstall
zstyle ':completion:*' expand prefix suffix
zstyle ':completion:*' file-sort access
zstyle ':completion:*' list-prompt %SAt %p: Hit TAB for more, or the character to insert%s
zstyle ':completion:*' list-suffixes true
zstyle ':completion:*' matcher-list '' 'm:{[:lower:]}={[:upper:]} m:{[:lower:][:upper:]}={[:upper:][:lower:]} r:|[._-]=** r:|=**' 'l:|=* r:|=*'
zstyle ':completion:*' menu select=long
zstyle ':completion:*' preserve-prefix '//[^/]##/'
zstyle ':completion:*' select-prompt %SScrolling active: current selection at %p%s
zstyle ':completion:*' special-dirs true
zstyle :compinstall filename '/home/jaybee/.zshrc'

autoload -Uz compinit
compinit
